/**
 * main.nml
 */

width = 400.0
height = 400.0

Key module {

  up = & false
  down = & false
  left = & false
  right = & false
  z = & false

  onSpKeyDown(#key #x #y) = {
    key match {
      | Glut.KEY_UP    => up := true
      | Glut.KEY_DOWN  => down := true
      | Glut.KEY_LEFT  => left := true
      | Glut.KEY_RIGHT => right := true
      | _ => ()
    }
  }

  onSpKeyUp(#key #x #y) = {
    key match {
      | Glut.KEY_UP    => up := false
      | Glut.KEY_DOWN  => down := false
      | Glut.KEY_LEFT  => left := false
      | Glut.KEY_RIGHT => right := false
      | _ => ()
    }
  }

  onKeyDown(#key #x #y) = {
    char_of_int(key) match {
      | 'z' => z := true
      | _ => ()
    }
  }

  onKeyUp(#key #x #y) = {
    char_of_int(key) match {
      | 'z' => z := false
      | _ => ()
    }
  }

  init() = {
    Glut.specialFunc(onSpKeyDown)
    Glut.specialUpFunc(onSpKeyUp)
    Glut.keyboardFunc(onKeyDown)
    Glut.keyboardUpFunc(onKeyUp)
  }
}

Ship module {
  rsize = 25.
  x = & width /. 2.
  y = & (height -. rsize *. 2.0)
  speed = 5.

  bullets:ref[list[(float,float)]] = & []

  addBullet(x y) = {
    bullets := (x,y):: (*bullets)
  }
  moveBullets(bs) = {
    bullets := (*bullets) |>
      List.map{|(x,y)=> (x,y-.10.) }
    bullets := (*bullets) |>
      List.filter{|(_,y)=>y > 20. }
  }
  drawBullets() = {
    (*bullets)|>List.iter{|(x,y)=>
      GlDraw.rect(
        x -. 1.5, y -. 1.5
        x +. 3., y +. 3.
      )
    }
  }

  move() = {

    m = 5
    m = if(*Key.left)  m - 1 else m
    m = if(*Key.right) m + 1 else m
    m = if(*Key.up)    m - 3 else m
    m = if(*Key.down)  m + 3 else m
    speed = m match {
      | 1
      | 3
      | 7
      | 9 => speed /. 1.4
      | _ => speed
    }
    nx,ny = m match {
      | 1 => *x -. speed, *y -. speed
      | 2 => *x         , *y -. speed
      | 3 => *x +. speed, *y -. speed
      | 4 => *x -. speed, *y
      | 5 => *x         , *y
      | 6 => *x +. speed, *y
      | 7 => *x -. speed, *y +. speed
      | 8 => *x         , *y +. speed
      | 9 => *x +. speed, *y +. speed
      | _ => *x         , *y
    }

    x := min(max(nx rsize /. 2.) width -. rsize /. 2.)
    y := min(max(ny rsize /. 2.) height -. rsize /. 2.)

    moveBullets()
    if(*Key.z) addBullet(*x ; *y -. rsize /. 2.)

  }


  draw() = {
    GlDraw.color(1.0, 0.0, 0.0)
    GlDraw.rect(
      *x -. rsize /. 2., *y -. rsize /. 2.
      *x +. rsize /. 2., *y +. rsize /. 2.
    )
    GlDraw.color(0.5, 1.0, 1.0)
    drawBullets()
  }
}

Game module {

  draw() = {
    GlClear.clear([`color])
    Ship.draw()
    Glut.swapBuffers()
  }

  def timer(#value) = {
    Glut.postRedisplay()
    Ship.move()
    Glut.timerFunc(ms = 15 cb = timer value = 1)
  }

  resize(#w; #h) = {
    h = max(h; 1)
    GlDraw.viewport(x=0; y=0; w=w; h=h)

    GlMat.mode(`projection)
    GlMat.load_identity()

    ortho = GlMat.ortho(z=(1.0, -1.0))
    x = (0.0, width)
    y = (height, -0.0)
    ortho(x=x; y=y)

    GlMat.mode(`modelview)
    GlMat.load_identity()
  }

  main = {
    _ = Glut.init(Sys.argv)
    Glut.initDisplayMode(double_buffer = true; alpha= true)()
    Glut.initWindowSize(w= int_of_float(width); h= int_of_float(height))
    _ = Glut.createWindow("Game01")
    Glut.displayFunc(draw)
    Glut.reshapeFunc(resize)
    Key.init()
    Glut.timerFunc(ms=33 cb=timer value=1)
    GlClear.color(alpha=1.0; (0.1, 0.1, 0.1))
    Glut.mainLoop()
  }
}



